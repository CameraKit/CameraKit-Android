apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.ext.versionName
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/base'
        main.java.srcDirs += 'src/main/api16'
        main.java.srcDirs += 'src/main/api21'
        main.java.srcDirs += 'src/main/types'
        main.java.srcDirs += 'src/main/events'
        main.java.srcDirs += 'src/main/utils'
        main.java.srcDirs += 'src/main/vision'
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
}

dependencies {
    //noinspection GradleCompatible
    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.android.support:exifinterface:27.1.0'
    testCompile 'org.mockito:mockito-core:2.17.0'
    testCompile 'junit:junit:4.12'
    implementation 'com.google.android.gms:play-services-vision:12.0.0'
}

group = 'com.wonderkiln'
version = rootProject.ext.versionName

install {
    repositories.mavenInstaller {
        pom.project {
            name 'CameraKit-Core-Android'
            packaging 'aar'
        }
    }
}

bintray {
    user = rootProject.ext.bintrayUser
    key = rootProject.ext.bintrayKey
    override = true
    publish = true
    configurations = ['archives']
    pkg {
        repo = 'snapshots'
        name = 'CameraKit-Core-Android'
        userOrg = 'wonderkiln'
        vcsUrl = 'https://github.com/wonderkiln/CameraKit-Android.git'
        version {
            name = rootProject.ext.versionName
            vcsTag = rootProject.ext.versionName
            released = new Date()
        }
    }
}
